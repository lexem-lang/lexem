
--------------------------
-- Lexem grammar v0.2.0 --
--------------------------
-- Description:
--    Loop statements of the functional core.
--------------------------

let (ws, ws_simple, check_keyword, identifier_dynamic) = import("root:commons")
let fn_exp = import("../expressions")
let fn_stmt = import("../statements")
let (destructuring) = import("variable_declarations")

pub! exp infinite {
    |> check_keyword("repeat")
    |? ws? index:identifier_dynamic

    onBack {
        log.error("The infinite loop statement requires a block statement ({}) for its 'then' block.")
    }

    |> ws? then:global.lexem.block

    |? ws? clauses:last_clauses
}

pub! exp conditional {
    |? check_keyword("repeat") ws? index:identifier_dynamic ws?
    | check_keyword("while") set_props![while]
    | check_keyword("until") set_props![- while]

    onBack {
        log.error("The conditional loop statement requires an expression for its condition.")
    }

    |> ws? condition:fn_exp.expression

    onBack {
        log.error("The conditional loop statement requires a block statement ({}) for its 'then' block.")
    }

    |> ws? then:global.lexem.block

    |? ws? clauses:last_clauses
}

pub! exp iterator {
    |? check_keyword("repeat") ws? index:identifier_dynamic ws?
    |> check_keyword("for")

    onBack {
        log.error("The iterator loop statement requires at least a variable name for the current value of the iteration.")
    }

    |> ws?
    | variable:destructuring
    | variable:identifier_dynamic

    onBack {
        log.error("The iterator loop statement requires the 'in' symbol after the variable names.")
    }

    |> ws? check_keyword("in")

    onBack {
        log.error("The iterator loop statement requires an expression to get the iterator.")
    }

    |> ws? condition:fn_exp.expression

    onBack {
        log.error("The iterator loop statement requires a block statement ({}) for its body.")
    }

    |> ws? then:global.lexem.block

    |? ws? clauses:last_clauses
}

pub! last_clauses {
    var preValue = false
    |+ end:last_clause("last") \(preValue = true)
    |+ (\(? preValue) ws?)? end:last_clause("else")
}



--------------
--  Others  --
--------------

exp last_clause(keyword) {
    |> check_keyword(keyword)

    onBack {
        log.error("The \(keyword) clause of the loop statement requires a block statement ({}) for its body.")
    }

    |> ws? body:global.lexem.block
}