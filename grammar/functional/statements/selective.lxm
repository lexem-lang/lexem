
#------------------------#
#- Lexem grammar v0.1.1 -#
#------------------------#
#- Description:
#-    Selective statements of the functional core.
#------------------------#

let (ws, ws_eol, check_keyword, identifier) = import("../../commons")
let fn_exp = import("./expressions")
let fn_stmt = import("../statements")
let (destructuring) = import("variable_declarations")

pub! exp selective {
    |> check_keyword("when")
    |? ws? condition:fn_exp.expression

    onBack {
        log.error("The selective statement requires here the open bracket '{'.")
    }

    |> ws? "{"
    |? "'" tag:identifier

    for *+ {
        |> ws? case
    }
    
    onBack {
        log.error("The selective statement requires here the close bracket '}'.")
    }

    |> ws? "}"
}

exp case {
    repeat i for ++ {
        if i > 0 {
            |> ws? "," ws?
        }

        | pattern_conditional
        | pattern_else
        | pattern_variable
        | pattern_expression
    }

    onBack {
        log.error("The selective case requires a block statement ({}) for its body.")
    }

    |> ws? code:fn_stmt.block ws_eol
}

exp pattern_variable {
    | check_keyword("var") set_props![- isConst]
    | check_keyword("let") set_props![isConst]

    onBack {
        log.error("An identifier or destructuring is required for the var declaration pattern.")
    }

    | ws? name:identifier
    | ws? destructuring

    |? ws? conditional:pattern_conditional
}

exp pattern_expression {
    |> value:fn_exp.expression
    |? ws? conditional:pattern_conditional
}

exp pattern_conditional {
    | check_keyword("if") set_props![if]
    | check_keyword("unless") set_props![- if]

    onBack {
        log.error("The conditional pattern requires an expression for its condition.")
    }

    |> ws? condition:fn_exp.expression
}

exp pattern_else {
    |> check_keyword("else")
}

