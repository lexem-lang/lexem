
#------------------------#
#- Lexem grammar v0.1.0 -#
#------------------------#
#- Description:
#-    The lexem's function literals.
#------------------------#

let {sml} = import("generics")
let fn_exp = import("functional/expressions")
let fn_stmt = import("functional/statement")

pub! exp function {
	|> "fun" args:function_arguments?

	onback {
		log.error("Incorrect literal function. It is required a body block '{}'.")
	}
	|> body:fn_stmt.block
}

pub! exp function_arguments {
	|> "(" sml*
	|? (elements:function_arguments_element /infix: sml* "," sml*)+

	onback {
		log.error("Incorrect arguments. The close parenthesis ')' is required.")
	}
	|> sml* ("," sml*)? ")"	#- trailing comma
}

exp function_arguments_element {
	|> key:identifier
	|? sml* ":" {
		onback {
			log.error("Incorrect argument. After the relation symbol ':' it is require the default value of the argument.")
		}
		|> sml* value:fn_exp.expression
	}
}