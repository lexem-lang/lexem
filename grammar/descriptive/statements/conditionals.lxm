
#------------------------#
#- Lexem grammar v0.1.0 -#
#------------------------#
#- Description:
#-    Conditional statements of the lexem's descriptive core.
#------------------------#

let {sml, ..gen} = import("generics")
let fn_exp = import("functional/expressions")
let exp_stmt = import("../statements")


pub! exp conditional {
    | gen.keyword("if") set_props![if]
    | gen.keyword("unless") set_props![- if]

    onback {
        log.error("The conditional statement requires an expression for its condition.")
    }
    |> sml* condition:fn_exp.expression

    onback {
        log.error("The conditional statement requires a block statement ({}) for its 'then' block.")
    }
    |> sml* then:exp_stmt.block

    |? sml* "else" {
        onback {
            log.error("The else clause of a conditional statement requires a block statement ({}) for its 'else' block.")
        }
        | sml* else:conditional
        | sml* else:exp_stmt.block
    }
}